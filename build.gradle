buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-contacts-plugin:3.0.1'
        classpath 'com.netflix.nebula:nebula-release-plugin:6.0.2'
        classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.5"
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.3.1'
}

group 'com.blackbuild.intellij'

ext.isReleaseVersion = !version.endsWith("-SNAPSHOT")

apply plugin: 'java-library'
apply plugin: 'jacoco'
apply plugin: 'nebula.release'
apply plugin: 'license'
apply plugin: "org.sonarqube"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'io.takari', name: 'maven-wrapper', version: '0.4.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

intellij {
    plugins 'maven'
}

patchPluginXml {
    untilBuild "182.*"

    changeNotes """
      - Switch to Event Log notifications
      - update to 2018.2 EAP
    """
}

publishPlugin {
    username findProperty("jetbrainsUsername")
    password findProperty("jetbrainsPassword")
    if (status != 'release') channels([status])
}

tasks.postRelease.dependsOn publishPlugin
